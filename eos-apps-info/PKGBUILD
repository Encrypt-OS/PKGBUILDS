# Maintainer: Encrypt-OS <info@Encrypt-OS.com>

pkgname=eos-apps-info
pkgdesc="Documentation about apps in the Encrypt-OS repository."
pkgver=1.2.5
pkgrel=1
arch=('any')
license=('GPL')
depends=(
  bash
  yad-eos
)
optdepends=(
  'pandoc: generate html pages from github-markdown and man files'
  'imagemagick: save space with picture files'
)

_appsinfodir=etc/${pkgname}

backup=(
  $_appsinfodir/${pkgname}.conf
)
url=https://github.com/Encrypt-OS/PKGBUILDS/tree/master/${pkgname}
_url="https://raw.githubusercontent.com/Encrypt-OS/PKGBUILDS/master/${pkgname}"

source=(
  $_url/${pkgname}
  $_url/${pkgname}-helper
  $_url/${pkgname}-collect   # not installed as app, used only under PKGBUILDS/eos-apps-info
  $_url/${pkgname}.desktop
  $_url/${pkgname}.conf
  $_url/${pkgname}-pages.tar.gz
)

sha512sums=('f589d772c9821a514fceef4d1487edc10ae4fe5e89581a669bcd509b58e10ed9510a70b4a02460d32462c7c493a980142787177ce116e5b21c9504fd67793c67'
            'f94c3ebc2d7d4ef47c0209c538c0a86551789e8ebf095f5323695632510d5aee9e3017145e49e056890ce27d362afae58db132866af2b263d826cc1837046d8f'
            'a76f677731c03719d70f7433f838e822c5f42750de5ae0c13a2f63b91b5ab793d566b7b091bc260ccf9d97215bfefcdb1f7a9fdbc8a60a8dd6659bd0e7c02336'
            '3d8982cf378588b6d74c46035bcdbd048c05ad759000d9ead00da1e7fe27428c117b5f5eec225044ea70909666337a5a71830602514dd432d8d78bce64023267'
            '4b5eef3ce297cfa66d38b6ad92739eee905050228f72bd2c58d7f05700269b0ef434b0aa5c6ff8423a98af33918089f1255858bd2385ef3db2967d71e0d1d350'
            'aabcff236233dae46cc38a1d7d368014d100b8a7111352390edf0168b9d7b0aec24a70e23c554c20a795b104cae51042bb07f18f588c1dba804dcebb8b329403')

package() {
  local xx

  install -Dm755 ${pkgname}                  $pkgdir/usr/bin/${pkgname}
  install -Dm755 ${pkgname}-helper           $pkgdir/usr/bin/${pkgname}-helper
  install -Dm644 ${pkgname}.desktop          $pkgdir/usr/share/applications/${pkgname}.desktop
  install -Dm644 ${pkgname}.conf             $pkgdir/$_appsinfodir/${pkgname}.conf

  install -d $pkgdir/$_appsinfodir

  for xx in pageinfo/*/* ; do
      case "$xx" in
          *.html | *.png)
              install -Dm644 "$xx" "$pkgdir/$_appsinfodir/$xx" ;;
          *)
              echo "$pkgname: warning: unsupported page '$xx'" >&2 ;;
      esac
  done
}
