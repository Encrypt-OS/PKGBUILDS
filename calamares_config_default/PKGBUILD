# Maintainer: SnailDOS <encryptos@snaildos.com>


pkgname=calamares_config_default
pkgver=26.20.1.1
pkgrel=1
_repo_name=EncryptOS-calamares
pkgdesc='Encrypt-OS calamares configuration files for Community Editions'
arch=('any')
url='https://www.Encrypt-OS.com'
license=('GPL3')
source=("https://github.com/Encrypt-OS/${_repo_name}/archive/refs/tags/${pkgver}.tar.gz")

sha512sums=('22b6afe69929abd6cb2baf6466fe5eb60e892c029f701429725360913a1c03c9e4ad0df1dd148b456509f5740b516edda737741a73e3b89b3a343296dd836ea7')


_date=$(date +%Y.%m.%d)

prepare() {
    mv "$_repo_name-$pkgver"            "$_repo_name"

    # set date for calamares_branding
    sed -i "$srcdir/${_repo_name}/calamares/branding/EncryptOS/branding.desc" \
    -e "s|^\(    version:[ ]*\).*$|\1$_date|" \
    -e "s|^\(    shortVersion:[ ]*\).*$|\1$_date|"

    chmod +x "$srcdir/${_repo_name}/calamares/scripts/"{cleaner_script.sh,chrooted_cleaner_script.sh,update-mirrorlist,pacstrap_calamares}
}

package() {
    install -dm 755 "$pkgdir/etc/calamares"
    cp -r --no-preserve=ownership "$srcdir/${_repo_name}/calamares" "$pkgdir/etc/"
}
