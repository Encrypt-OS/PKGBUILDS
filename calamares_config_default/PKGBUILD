# Maintainer: SnailDOS <encryptos@snaildos.com>


pkgname=calamares_config_default
pkgver=10.1.0
pkgrel=1
_repo_name=EncryptOS-calamares
pkgdesc='Encrypt-OS calamares configuration files for Community Editions'
arch=('any')
url='https://www.Encrypt-OS.com'
license=('GPL3')
source=("https://github.com/Encrypt-OS/${_repo_name}/archive/refs/tags/${pkgver}.tar.gz")

sha512sums=('aaf38d9bdb29d11d3d996defcf01edf5daa03ac2e04c6755cbded0a6ac6ada7fba4202d68e1034aeda7932aef7a03b3c7d0bf9ce6b3ecf96b475c46644fef092')


_date=$(date +%Y.%m.%d)

prepare() {
    mv "$_repo_name-$pkgver"            "$_repo_name"

    # set date for calamares_branding
    sed -i "$srcdir/${_repo_name}/calamares/branding/EncryptOS/branding.desc" \
    -e "s|^\(    version:[ ]*\).*$|\1$_date|" \
    -e "s|^\(    shortVersion:[ ]*\).*$|\1$_date|"

    chmod +x "$srcdir/${_repo_name}/calamares/scripts/"{cleaner_script.sh,chrooted_cleaner_script.sh,update-mirrorlist,pacstrap_calamares}

    rm "$srcdir/${_repo_name}/calamares/modules/netinstall-ce-base.yaml"
    rm "$srcdir/${_repo_name}/calamares/settings_community.conf"
    rm "$srcdir/${_repo_name}/calamares/modules/"{packagechooser_ce.conf,netinstall_community_base.conf}
    rm -R "$srcdir/${_repo_name}/calamares/images-ce"
}

package() {
    install -dm 755 "$pkgdir/etc/calamares"
    cp -r --no-preserve=ownership "$srcdir/${_repo_name}/calamares" "$pkgdir/etc/"
}
