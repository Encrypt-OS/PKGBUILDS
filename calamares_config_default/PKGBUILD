# Maintainer: SnailDOS <encryptos@snaildos.com>


pkgname=calamares_config_default
pkgver=26.05.1.1
pkgrel=1
_repo_name=EncryptOS-calamares
pkgdesc='Encrypt-OS calamares configuration files for Community Editions'
arch=('any')
url='https://www.Encrypt-OS.com'
license=('GPL3')
source=("https://github.com/Encrypt-OS/${_repo_name}/archive/refs/tags/${pkgver}.tar.gz")

sha512sums=('3fbc550d91eae1aa35fedf0df0248af31c6115c1efee16f31c8964fcd3122c6548df3a4da6889fe592dc09ce6e24151bed277b1c32aecf5c349013d09a6a581d')


_date=$(date +%Y.%m.%d)

prepare() {
    mv "$_repo_name-$pkgver"            "$_repo_name"

    # set date for calamares_branding
    sed -i "$srcdir/${_repo_name}/calamares/branding/EncryptOS/branding.desc" \
    -e "s|^\(    version:[ ]*\).*$|\1$_date|" \
    -e "s|^\(    shortVersion:[ ]*\).*$|\1$_date|"

    chmod +x "$srcdir/${_repo_name}/calamares/scripts/"{cleaner_script.sh,chrooted_cleaner_script.sh,update-mirrorlist,pacstrap_calamares}

    rm "$srcdir/${_repo_name}/calamares/modules/netinstall-ce-base.yaml"
    rm "$srcdir/${_repo_name}/calamares/settings_community.conf"
    rm "$srcdir/${_repo_name}/calamares/modules/"{packagechooser_ce.conf,netinstall_community_base.conf}
    rm -R "$srcdir/${_repo_name}/calamares/images-ce"
}

package() {
    install -dm 755 "$pkgdir/etc/calamares"
    cp -r --no-preserve=ownership "$srcdir/${_repo_name}/calamares" "$pkgdir/etc/"
}
