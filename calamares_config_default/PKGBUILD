# Maintainer: SnailDOS <encryptos@snaildos.com>


pkgname=calamares_config_default
pkgver=26.13.1.1
pkgrel=1
_repo_name=EncryptOS-calamares
pkgdesc='Encrypt-OS calamares configuration files for Community Editions'
arch=('any')
url='https://www.Encrypt-OS.com'
license=('GPL3')
source=("https://github.com/Encrypt-OS/${_repo_name}/archive/refs/tags/${pkgver}.tar.gz")

sha512sums=('3b0b894f64cf2ce7be5529675e465257ebd6cdbb0ebaf946b7d12d952fa3b0c75da8fc846ade6fdf6ce5939b8cefe836929ebf8266930241a72974358afad9bc')


_date=$(date +%Y.%m.%d)

prepare() {
    mv "$_repo_name-$pkgver"            "$_repo_name"

    # set date for calamares_branding
    sed -i "$srcdir/${_repo_name}/calamares/branding/EncryptOS/branding.desc" \
    -e "s|^\(    version:[ ]*\).*$|\1$_date|" \
    -e "s|^\(    shortVersion:[ ]*\).*$|\1$_date|"

    chmod +x "$srcdir/${_repo_name}/calamares/scripts/"{cleaner_script.sh,chrooted_cleaner_script.sh,update-mirrorlist,pacstrap_calamares}
}

package() {
    install -dm 755 "$pkgdir/etc/calamares"
    cp -r --no-preserve=ownership "$srcdir/${_repo_name}/calamares" "$pkgdir/etc/"
}
