# Maintainer: SnailDOS <encryptos@snaildos.com>


pkgname=calamares_config_default
pkgver=32.22.1.1
pkgrel=1
_repo_name=EncryptOS-calamares
pkgdesc='Encrypt-OS calamares configuration files'
arch=('any')
url='https://www.Encrypt-OS.com'
license=('GPL3')
source=("https://github.com/Encrypt-OS/${_repo_name}/archive/refs/tags/${pkgver}.tar.gz")

sha512sums=('0c9384028e21d756cc821f249b81818175eef10614264b68c5e6dea35561d4f48561470790876bdcd1dbcfcf87398b4ef3c53a84e69836b1f51ce4409c752513')


_date=$(date +%Y.%m.%d)

prepare() {
    mv "$_repo_name-$pkgver"            "$_repo_name"

    # set date for calamares_branding
    sed -i "$srcdir/${_repo_name}/calamares/branding/EncryptOS/branding.desc" \
    -e "s|^\(    version:[ ]*\).*$|\1$_date|" \
    -e "s|^\(    shortVersion:[ ]*\).*$|\1$_date|"

    chmod +x "$srcdir/${_repo_name}/calamares/scripts/"{cleaner_script.sh,chrooted_cleaner_script.sh,update-mirrorlist,pacstrap_calamares}
}

package() {
    install -dm 755 "$pkgdir/etc/calamares"
    cp -r --no-preserve=ownership "$srcdir/${_repo_name}/calamares" "$pkgdir/etc/"
}
