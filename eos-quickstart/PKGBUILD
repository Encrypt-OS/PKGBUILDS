pkgname=eos-quickstart
pkgver=1.4
pkgrel=1
pkgdesc="An application for getting a quick start with installing packages"
arch=('any')
license=('GPL3')
depends=('qt6-base' 'qt6-declarative' 'polkit')
optdepends=('konsole' 'gnome-terminal' 'xfce4-terminal' 'xterm' 'alacritty' 'lxterminal')
makedepends=('git' 'cmake' 'qt6-tools')
backup=(etc/eos-quickstart.conf)
source=(https://github.com/Encrypt-OS/encryptos-quickstart/archive/refs/tags/${pkgver}.tar.gz)
sha256sums=('f2a1d9ead44ad00b15300da08871add3dcb731c30cf0d5f5fa02c75227a75b14')

build() {
    cd "$srcdir/encryptos-quickstart-$pkgver"
    cmake -B build -S . -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE='Release'
    make -C build
}

package() {
    cd "$srcdir/encryptos-quickstart-$pkgver"
    make -C build DESTDIR="$pkgdir" install

    install -Dm0644 "$srcdir/encryptos-quickstart-$pkgver/eos-quickstart.conf" "$pkgdir/etc/eos-quickstart.conf"
}
