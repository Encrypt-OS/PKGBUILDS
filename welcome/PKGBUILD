# Maintainer: EncryptOS <info@EncryptOS.com>

pkgname=welcome
pkgdesc="Welcome greeter for new EncryptOS users."
pkgver=3.17.33
pkgrel=2
arch=('any')
license=('GPL')
depends=(
    systemd eos-bash-shared eos-translations
    eos-quickstart
)
optdepends=(
  'translate-shell: for generating a language translation for the User Interface'
  'reflector-simple: for the Update Mirrors button'
  'yad: upstream yad provider'
  'yad-eos: alternative yad provider (includes fixes)'
  'kdiff3: alternative app for pacdiff to use'
  'meld: alternative app for pacdiff to use'
  'diffuse: alternative app for pacdiff to use'
  'vim: alternative app for pacdiff to use'
)

url=https://github.com/EncryptOS/PKGBUILDS/tree/master/$pkgname
_url="https://raw.githubusercontent.com/EncryptOS/PKGBUILDS/master/$pkgname"

source=(
  $_url/$pkgname
  $_url/$pkgname.desktop
  $_url/wallpaper-once
  $_url/wallpaper-once.desktop
  $_url/eos-kill-yad-zombies
  $_url/welcome-dnd
  $_url/eos-install-mode-run-calamares
)
sha512sums=('48c78699709be63faad79426933f8f90c05d560b8197bdda503e67a4dbc7186eb97a8f5e9fce6f79d63d0516040ed828f6b5cc022ec304ff0c55ef914211db88'
            'fb7aabb18e4684f1a9734fe964f200d039258e9366ac5f96e82e04a1c467183897a396d175fef740269cb7368fb72db3fb38b085bd530c2655db448455a79df2'
            '8f586e5ddbfbf45533fa8a1d6faa43c395e25ce2fb7e06c97349a742eeea06edc06904fd61d28361ef912aefaa11d973d63fc141c74484d9b5bfdf1e4020581a'
            '10222f67ee49dd7fe61c5ce1e8db5f8ccbc096c7808c26c44a1dabaec006fca983c0beaf252a89930a4c7f32b2e2a218d1bd949f32a4a87cddd6d7676f3ca044'
            '90dfc1f08a428787305d72a2e439a377aee0bc095fcb0a2efb4245a15285d4b96b5e0ac644c02f27ab68aebbfc5c64e6ab7d733d9db8952d2999d589410d3b9e'
            'a8313ea0a289c1c1d5607bd3d4259a08cc5b8a2fed970197938967299ecfe06b8c12846796996593f1ea1b5ff7c842bd2b7f30776e8454422aa2c3692fc1d52b'
            '2c24de022758cc86bfa1d82d93c13dc696848b1a8a4c20c3e8894148500a44236443a6c5d9d3943bfa30739c16c807b9cef435c99b0fb1330e1f8bb5bbf0ac9c')
package() {
  install -d $pkgdir/usr/share/EncryptOS/scripts
  install -Dm755 $pkgname                 $pkgdir/usr/share/EncryptOS/scripts/$pkgname
  install -Dm755 wallpaper-once           $pkgdir/usr/share/EncryptOS/scripts/wallpaper-once

  install -d $pkgdir/usr/bin
  ln -s /usr/share/EncryptOS/scripts/$pkgname $pkgdir/usr/bin/eos-$pkgname

  install -Dm755 eos-kill-yad-zombies           $pkgdir/usr/bin/eos-kill-yad-zombies
  install -Dm755 welcome-dnd                    $pkgdir/usr/bin/welcome-dnd
  install -Dm755 eos-install-mode-run-calamares $pkgdir/usr/bin/eos-install-mode-run-calamares

  install -d $pkgdir/etc/xdg/autostart
  install -Dm644 wallpaper-once.desktop   $pkgdir/etc/xdg/autostart/wallpaper-once.desktop
  install -Dm644 $pkgname.desktop         $pkgdir/etc/xdg/autostart/$pkgname.desktop      # no --once

  install -d $pkgdir/usr/share/applications
  cp -a $pkgname.desktop $pkgname.desktop-enable
  sed -i $pkgname.desktop-enable \
      -e 's|^\(Exec=.* --startdelay.*\)$|#\1|' \
      -e 's|^#\(Exec=.* --once.*\)$|\1|'
  install -Dm644 $pkgname.desktop-enable         $pkgdir/usr/share/applications/$pkgname.desktop      # has --once
}
