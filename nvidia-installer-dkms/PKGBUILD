# Original Maintainer: Gustau Castells <karasu@antergos.com>
# Maintainer: EncryptOS

pkgname=nvidia-installer-dkms
pkgver=3.5
pkgrel=1
pkgdesc="Script to setup nvidia drivers (dkms version) in Encrypt-OS"
url=https://github.com/Encrypt-OS/PKGBUILDS/tree/master/$pkgname
_url=https://raw.githubusercontent.com/Encrypt-OS/PKGBUILDS/master/$pkgname
arch=('any')
license=('GPL2')
conflicts=(nvidia-installer)
depends=(
  python
  nvidia-installer-db
  nvidia-installer-common
  dkms
)

optdepends=(
  'linux-headers: for nvidia-installer-dkms with kernel linux'
  'linux-lts-headers: for nvidia-installer-dkms with kernel linux-lts'
  'linux-hardened-headers: for nvidia-installer-dkms with kernel linux-hardened'
  'linux-zen-headers: for nvidia-installer-dkms with kernel linux-zen'
)

source=(
  $_url/$pkgname
  $_url/nvidia-installer-check
)
sha512sums=('f2114e29a07e65f706b6d35a6987a6160664107dc4c82ff0c1350f4187be9bab90608c8dec78ab339625df222640563bc6fe82ab9f17b0064a4630f2b4b69c2a'
            '708d7a68dc2a86eeeea292cdba174f4d23441052f462d5954d6595b168b23bbaf4541ee56cc99326b8b6b52f26ec7812b5d0096f0722cc72b8867be34fa5f5ec')

package() {
  sed -i $pkgname \
      -e 's|^VERSION = .*$|VERSION = "'$pkgver'"|'

  install -Dm755 $pkgname                         $pkgdir/usr/bin/$pkgname
  install -Dm755 nvidia-installer-check           $pkgdir/usr/bin/nvidia-installer-check
}
